buildscript {
    ext {
        springBootVersion = '2.1.8.RELEASE'
        mooworkVersion = '1.2.0'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
        classpath("com.moowork.gradle:gradle-node-plugin:${mooworkVersion}")
    }
}

allprojects {
    group = 'com.club.masterpiece'
    version = '0.1.0' + '-' + new Date().format("yyyyMMdd-HHmmss")
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.8

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.moowork.node'

    repositories {
        mavenCentral()
    }

    dependencies {

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    configurations {
        developmentOnly

        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}

project(':movie-masterpiece-club-common') {
    dependencies {
    }
}

project(':movie-masterpiece-club-web') {
    dependencies {
        compile project(':movie-masterpiece-club-common')
    }

    node {
        download = true
        nodeModulesDir = file("./movie-masterpiece-club-web/frontend")
    }

    task setup(type: NpmTask) {
        description = "install node.js packages."
        args = ['install']
        inputs.files file ('package.json')
        outputs.files file ('node_modules')
    }   

    task buildFrontEnd(type: NpmTask, dependsOn: setup) {
        description = "bulid vue.js production mode"
        args = ['run', 'build:prod']
    }

    processResources.dependsOn 'buildWebFrontEnd'
}

project(':movie-masterpiece-club-admin') {
    dependencies {
        compile project(':movie-masterpiece-club-common')
    }
}